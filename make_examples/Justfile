[private]
default:
    just --list

publish:
    cp _build/*.md ../docs
    cp _build/images/*.png ../docs/images

clean:
    rm -rf _build

rebuild: clean build

build: (page 'factorial') (page 'arithmetic') (page 'cosine_addition') (page 'json_blob') (page 'let') (page 'config')


page STEM: (generate STEM)
    python3 make_page.py --stem "{{STEM}}"
    
generate STEM: (xml STEM) (json STEM) (yaml STEM) (mermaid STEM) (dot STEM)
    cp "{{STEM}}.mg" "_build/{{STEM}}.mg"

[private]
mkbuild:
    mkdir -p _build

MG := "../go/monogram/monogram"

[private]
xml STEM: mkbuild
    {{MG}} --one -f xml -i {{STEM}}.mg > _build/{{STEM}}.xml

[private]
json STEM: mkbuild
    {{MG}} --one -f json -i {{STEM}}.mg > _build/{{STEM}}.json

[private]
yaml STEM: mkbuild
    {{MG}} --one -f yaml -i {{STEM}}.mg > _build/{{STEM}}.yaml

[private]
mermaid STEM: mkbuild
    {{MG}} --one -f mermaid -i {{STEM}}.mg > _build/{{STEM}}.mermaid

[private]
dot STEM:
    mkdir -p _build/images
    {{MG}} --one -f dot -i {{STEM}}.mg > _build/{{STEM}}.dot
    dot -Tpng _build/{{STEM}}.dot -o _build/images/{{STEM}}.png


